!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols

%<n%>                        ! Toka                          ! Sustantivo
%<np%>
%<v%>                        ! 
%<num%>                      ! 
%<prn%>                      !
%<det%>                      !
%<adv%>                      !
%<adj%>                      ! 
%<cnjcoo%>                   !
%<cnjsub%>                   !
%<aux%>                      !
%<x%>                        !
%<cm%>                       !
%<sent%>                     !

%<imp%>                      !

%{y%}                        !
%{i%}                        ! 
%{o%}                        ! 
%{n%}                        !
%{N%}                        ! 
%> 

@P.PLUR.1@                   !
@P.SUBJ.1@                   !
@P.OBJ.1@                    !
@P.IOBJ.1@                   !
@R.PLUR.1@                   !
@R.SUBJ.1@                   !
@R.OBJ.1@                    !
@R.IOBJ.1@                   !
@D.PLUR@                     !
@D.SUBJ@                     !
@D.OBJ@                      !
@D.IOBJ@                     !

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

                                                   Punctuación ; 
                                                   Sustantivos ;
                                                   Verbos ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Verbos 
 
                                                   AGR-SUBJ-SIO ;
                                                   AGR-ZERO ;
                                                   AGR-IMPER ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON AGR-ZERO 

%<s_sg3%>:0%>                                      INCORP-ADV ;

LEXICON AGR-IMPER

%<imp%>%<s_sg2%>:x%{i%}%>                          AGR-SUBJ-FLAGS ;
%<imp%>%<s_pl2%>:x%{i%}%>                          AGR-SUBJ-FLAGS-PLUR ;

LEXICON AGR-SUBJ-SIO

%<s_sg1%>:ni%>                                     AGR-SUBJ-FLAGS ;
%<s_sg2%>:ti%>                                     AGR-SUBJ-FLAGS ;
%<s_sg3%>:0%>                                      AGR-SUBJ-FLAGS ;
%<s_pl1%>:ti%>                                     AGR-SUBJ-FLAGS-PLUR ;
%<s_pl2%>:nan%>                                    AGR-SUBJ-FLAGS-PLUR ;
%<s_pl3%>:0%>                                      AGR-SUBJ-FLAGS-PLUR ;

LEXICON AGR-SUBJ-FLAGS-PLUR

@P.PLUR.1@:@P.PLUR.1@                              AGR-SUBJ-FLAGS ;

LEXICON AGR-SUBJ-FLAGS

@P.SUBJ.1@:@P.SUBJ.1@                              AGR-OBJ-SO ; 
@P.SUBJ.1@:@P.SUBJ.1@                              AGR-OBJ-SI ; 
@P.SUBJ.1@:@P.SUBJ.1@                              INCORP-ADV ; 

!LEXICON AGR-SUBJ-NV
!
!%<s_sg1%>:ni%>                                     # ;
!%<s_sg2%>:ti%>                                     # ;
!%<s_sg3%>:0%>                                      # ;
!%<s_pl1%>:ti%>                                     # ;
!%<s_pl2%>:nan%>                                    # ;
!%<s_pl3%>:0%>                                      # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON AGR-OBJ-SI

%<i_sg1%>:nech%>                                   AGR-OBJ-SI-FLAGS ;
%<i_sg2%>:mits%>                                   AGR-OBJ-SI-FLAGS ;
%<i_sg2%>%<hon%>:mits%>on%>                        AGR-OBJ-SI-FLAGS ;
%<i_sg3%>:k%{i%}%>                                 AGR-OBJ-SI-FLAGS ;
%<i_pl1%>:tech%>                                   AGR-OBJ-SI-FLAGS ;
%<i_pl2%>:namech%>                                 AGR-OBJ-SI-FLAGS ;
%<i_pl3%>:ki%{N%}%>                                AGR-OBJ-SI-FLAGS ;
                                                   AGR-REFL ;

LEXICON AGR-OBJ-SI-FLAGS

@P.IOBJ.1@:@P.IOBJ.1@                              AGR-IOBJ-SIO ;

LEXICON AGR-OBJ-SO

%<o_sg1%>:nech%>                                   AGR-OBJ-SO-FLAGS ;
%<o_sg2%>:mits%>                                   AGR-OBJ-SO-FLAGS ;
%<o_sg2%>%<hon%>:mits%>on%>                        AGR-OBJ-SO-FLAGS ;
%<o_sg3%>:k%{i%}%>                                 AGR-OBJ-SO-FLAGS ;
%<o_pl1%>:tech%>                                   AGR-OBJ-SO-FLAGS ;
%<o_pl2%>:namech%>                                 AGR-OBJ-SO-FLAGS ;
%<o_pl3%>:ki%{N%}%>                                AGR-OBJ-SO-FLAGS ;
                                                   AGR-REFL ;

LEXICON AGR-OBJ-SO-FLAGS 
 
@P.OBJ.1@:@P.OBJ.1@                                INCORP-ADV ;

LEXICON AGR-REFL 

@P.IOBJ.1@%<i_ref%>:@P.IOBJ.1@mo%>                 AGR-IOBJ-SIO ;
@P.OBJ.1@%<o_ref%>:@P.OBJ.1@mo%>                   INCORP-ADV ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! ni-mits-in-maka-s

LEXICON AGR-IOBJ-SIO

%<o_sg3%>:                                         AGR-OBJ-SIO-FLAGS  ; 
%<o_pl3%>:i%{n%}%>                                 AGR-OBJ-SIO-FLAGS  ; 
                                                   INCORP-ADV ;
LEXICON AGR-OBJ-SIO-FLAGS

@P.OBJ.1@:@P.OBJ.1@                                INCORP-ADV ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON INCORP-ADV

%<yek%>:%{y%}ek%>                                  INCORP-DIR ;
%<tel%>:tel%>                                      INCORP-DIR ;
%<pan%>:pa%>                                       INCORP-DIR ;
%<pan%>:pa%{N%}%>                                  INCORP-DIR ;
%<sen%>:se%{N%}%>                                  INCORP-DIR ;
%<nal%>:nal%>                                      INCORP-DIR ;
                                                   INCORP-DIR ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON INCORP-DIR

%<on%>:on%>                                        INCORP-DIR-FLAGS ;
%<hual%>:hual%>                                    INCORP-DIR-FLAGS ;
                                                   BASES ;

LEXICON INCORP-DIR-FLAGS 

@D.OBJ@:@D.OBJ@                                    INCORP-INDEF ;
@D.OBJ@:@D.OBJ@                                    INCORP-NOMINAL ;
                                                   BASES ; 
                                                   

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON INCORP-INDEF

%<o_nn%>:ta%>                                      INCORP-INDEF-FLAGS ;
%<o_aa%>:te%>                                      INCORP-INDEF-FLAGS ;
                                                   INCORP-NOMINAL ;

LEXICON INCORP-INDEF-FLAGS 

@P.OBJ.1@:@P.OBJ.1@                                BASES ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON INCORP-NOMINAL

%<incorp%>:%{@%}%>                                 INCORP-NOMINAL-FLAGS ; 

LEXICON INCORP-NOMINAL-FLAGS 

@P.OBJ.1@:@P.OBJ.1@                                BASES ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON V-SIO-PRES 

%<v%>%<tv%>%<pres%>:                               V-AGR-PRES ;

LEXICON V-SIO-PRET

%<v%>%<tv%>%<pret%>:%>k                            V-AGR-PRET ;

LEXICON V-SIO-IMPF

%<v%>%<tv%>%<impf%>:%>ya                           V-AGR-PRES ;

LEXICON V-SIO-PROG

%<v%>%<tv%>%<prog%>%<pres%>:%{j%}%>to%>k           V-AGR-PRET ;
%<v%>%<tv%>%<prog%>%<fut%>:%{j%}%>to%>s            V-AGR-FUTR;
%<v%>%<tv%>%<prog%>%<impf%>:%{j%}%>to%>ya          V-AGR-PRES ;

LEXICON V-SIO-FUTR

%<v%>%<tv%>%<fut%>:%>s                             V-AGR-FUTR ;
%<v%>%<tv%>%<cni%>:%>s%>kia                        V-AGR-PRES ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON V-SO-PRES 

%<v%>%<tv%>%<pres%>:                               V-AGR-PRES ;

LEXICON V-SO-PRET

%<v%>%<tv%>%<pret%>:%>k                            V-AGR-PRET ;

LEXICON V-SO-IMPF

%<v%>%<tv%>%<impf%>:%>ya                           V-AGR-PRES ;

LEXICON V-SO-PROG

%<v%>%<tv%>%<prog%>%<pres%>:%>to%>k                V-AGR-PRET ;
%<v%>%<tv%>%<prog%>%<fut%>:%>to%>s                 V-AGR-FUTR;
%<v%>%<tv%>%<prog%>%<impf%>:%>to%>ya               V-AGR-PRES ;

LEXICON V-SO-FUTR

%<v%>%<tv%>%<fut%>:%>s                             V-AGR-FUTR ;
%<v%>%<tv%>%<cni%>:%>s%>kia                        V-AGR-PRES ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON V-SI-PRES 

%<v%>%<tv%>%<pres%>:                               V-AGR-PRES ;

LEXICON V-SI-PRET

%<v%>%<tv%>%<pret%>:%>k                            V-AGR-PRET ;

LEXICON V-SI-IMPF

%<v%>%<tv%>%<impf%>:%>ya                           V-AGR-PRES ;

LEXICON V-SI-PROG

%<v%>%<tv%>%<prog%>%<pres%>:%>to%>k                V-AGR-PRET ;
%<v%>%<tv%>%<prog%>%<fut%>:%>to%>s                 V-AGR-FUTR;
%<v%>%<tv%>%<prog%>%<impf%>:%>to%>ya               V-AGR-PRES ;

LEXICON V-SI-FUTR

%<v%>%<tv%>%<fut%>:%>s                             V-AGR-FUTR ;
%<v%>%<tv%>%<cni%>:%>s%>kia                        V-AGR-PRES ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON V-S-PRES 

%<v%>%<tv%>%<pres%>:                               V-AGR-PRES ;

LEXICON V-S-PRET

%<v%>%<iv%>%<pret%>:%>k                            V-AGR-PRET ;

LEXICON V-S-IMPF

%<v%>%<iv%>%<impf%>:%>ya                           V-AGR-PRES ;

LEXICON V-S-PROG

%<v%>%<iv%>%<prog%>%<pres%>:%>to%>k                V-AGR-PRET ;
%<v%>%<iv%>%<prog%>%<fut%>:%>to%>s                 V-AGR-FUTR;
%<v%>%<iv%>%<prog%>%<impf%>:%>to%>ya               V-AGR-PRES ;

LEXICON V-S-FUTR

%<v%>%<iv%>%<fut%>:%>s                             V-AGR-FUTR ;
%<v%>%<iv%>%<cni%>:%>s%>kia                        V-AGR-PRES ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON V-Ø-PRES

%<v%>%<iv%>%<pres%>:                               V-AGR-PRES ;

LEXICON V-Ø-PRET

%<v%>%<iv%>%<pret%>:%>k                            V-AGR-PRET ;

LEXICON V-Ø-IMPF

%<v%>%<iv%>%<impf%>:%>ya                           V-AGR-PRES ;

LEXICON V-Ø-PROG

%<v%>%<iv%>%<prog%>%<pres%>:%>to%>k                V-AGR-PRET ;
%<v%>%<iv%>%<prog%>%<fut%>:%>to%>s                 V-AGR-FUTR;
%<v%>%<iv%>%<prog%>%<impf%>:%>to%>ya               V-AGR-PRES ;

LEXICON V-Ø-FUTR

%<v%>%<iv%>%<fut%>:%>s                             V-AGR-FUTR ;
%<v%>%<iv%>%<cni%>:%>s%>kia                        V-AGR-PRES ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON V-AGR-PRET

@D.PLUR@:@D.PLUR@                                  # ; 
@R.PLUR.1@:@R.PLUR.1@%>ej                          # ; 

LEXICON V-AGR-PRES 

@D.PLUR@:@D.PLUR@                                  # ; 
@R.PLUR.1@:@R.PLUR.1@%>j                           # ; 

LEXICON V-AGR-FUTR

@D.PLUR@:@D.PLUR@                                  # ; 
@R.PLUR.1@:@R.PLUR.1@%>kej                         # ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON N-REL

%<n%>%<rel%>:                                      # ;
%<n%>%<rel%>%<hon%>:%>tsin                         # ;

LEXICON N-TL 

%<n%>%<abs%>:%>t                                   # ;
%<n%>%<hon%>:%>tsin                                # ;
%<n%>%<pl%>:%>mej                                  # ;
%<n%>%<hon%>%<pl%>:%>tsi%>tsin                     # ;
!%<n%>%<cmp%>%+:                                    # ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON BASES

@R.SUBJ.1@@R.OBJ.1@@R.IOBJ.1@:@R.SUBJ.1@@R.OBJ.1@@R.IOBJ.1@ SIO ;
@R.SUBJ.1@@R.OBJ.1@@D.IOBJ@:@R.SUBJ.1@@R.OBJ.1@@D.IOBJ@     SO ;
@R.SUBJ.1@@D.OBJ@@R.IOBJ.1@:@R.SUBJ.1@@D.OBJ@@R.IOBJ.1@     SI ;
@R.SUBJ.1@@D.OBJ@@D.IOBJ@:@R.SUBJ.1@@D.OBJ@@D.IOBJ@         S ;
@D.SUBJ@@D.OBJ@@D.IOBJ@:@D.SUBJ@@D.OBJ@@D.IOBJ@             Ø ;

LEXICON SIO
! V-*-PRES: Base I
! V-*-IMPF:
! V-*-PROG:
! V-*-PRET: Base II
! V-*-FUT:  Base III

! "dar"
maka:maka                                          V-SIO-PRES ; ! Base I
maka:maka                                          V-SIO-IMPF ;
maka:maka                                          V-SIO-PROG ;
maka:maka                                          V-SIO-PRET ; ! Base II
maka:maka                                          V-SIO-FUTR ; ! Base III

! "vender"
namaka:namaka                                      V-SIO-PRES ; ! Base I
namaka:namaka                                      V-SIO-IMPF ;
namaka:namaka                                      V-SIO-PROG ;
namaka:namaka                                      V-SIO-PRET ; ! Base II
namaka:namaka                                      V-SIO-FUTR ; ! Base III

! "llamar"
notsa:notsa                                        V-SIO-PRES ; ! Base I
notsa:notsa                                        V-SIO-IMPF ;
notsa:notsa                                        V-SIO-PROG ;
notsa:notsa                                        V-SIO-PRET ; ! Base II
notsa:notsa                                        V-SIO-FUTR ; ! Base III

! "tirar"
tamota:tamota                                      V-SIO-PRES ; ! Base I
tamota:tamota                                      V-SIO-IMPF ;
tamota:tamota                                      V-SIO-PROG ;
tamota:tamota                                      V-SIO-PRET ; ! Base II
tamota:tamota                                      V-SIO-FUTR ; ! Base III


LEXICON SO

! "tener"
piya:piya                                          V-SO-PRES  ; ! Base I
piya:piya                                          V-SO-IMPF  ;
piya:pix                                           V-SO-PROG  ;
piya:pix                                           V-SO-PRET  ; ! Base II
piya:piya                                          V-SO-FUTR  ; ! Base III

LEXICON SI 

! "dar de comer"
tamaka:ta%>maka                                    V-SI-PRES ; ! Base I
tamaka:ta%>maka                                    V-SI-IMPF ;
tamaka:ta%>maka                                    V-SI-PROG ;
tamaka:ta%>maka                                    V-SI-PRET ; ! Base II
tamaka:ta%>maka                                    V-SI-FUTR ; ! Base III

! "darse poder"
huelimaka:hueli%>maka                              V-SI-PRES ; ! Base I
huelimaka:hueli%>maka                              V-SI-IMPF ;
huelimaka:hueli%>maka                              V-SI-PROG ;
huelimaka:hueli%>maka                              V-SI-PRET ; ! Base II
huelimaka:hueli%>maka                              V-SI-FUTR ; ! Base III


LEXICON S

! "llorar"
choka:choka                                        V-S-PRES   ; ! Base I
choka:choka                                        V-S-IMPF   ;
choka:choka                                        V-S-PROG   ;
choka:choka                                        V-S-PRET   ; ! Base II
choka:choka                                        V-S-FUTR   ; ! Base III

LEXICON Ø

! "llover"
kiyohui:kiyohui                                    V-Ø-PRES   ; ! Base I
kiyohui:kiyohui                                    V-Ø-IMPF   ;
kiyohui:kiyohui                                    V-Ø-PROG   ;
kiyohui:kiyohui                                    V-Ø-PRET   ; ! Base II
kiyohui:kiyohui                                    V-Ø-FUTR   ; ! Base III

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Sustantivos 

                                                   Comunes ; 
                                                   Relacionales ; 

LEXICON Relacionales

huan:huan                                          N-REL ; ! "con"

LEXICON Comunes 

nakat:naka                                         N-TL ; ! "carne"

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Punctuación

%.%<sent%>:%. # ;
%.%.%<sent%>:%.%. # ;
%.%.%.%<sent%>:%.%.%. # ;
%!%<sent%>:%! # ;
%?%<sent%>:%? # ;
%¿%<sent%>:%¿ # ;
%¡%<sent%>:%¡ # ;
%,%<cm%>:%, # ;

